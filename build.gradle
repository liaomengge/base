plugins {
    id 'se.patrikerdes.use-latest-versions' version '0.2.15' apply false
    id 'com.github.ben-manes.versions' version '0.36.0' apply false
    id 'com.github.spotbugs' version '4.6.0' apply false
    id 'com.diffplug.spotless' version '5.10.2' apply false
    id 'io.spring.dependency-management' version '1.0.11.RELEASE' apply false
}

allprojects {
    wrapper {
        gradleVersion = '5.6.4'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven'
    apply plugin: 'idea'
    apply plugin: 'signing'
    apply plugin: 'maven-publish'
    apply plugin: 'project-report'
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'se.patrikerdes.use-latest-versions'
    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'io.spring.dependency-management'

    group = 'com.github.liaomengge'
    version = '3.0.0.RELEASE'

    apply from: "${rootProject.projectDir}/gradle/latest-version.gradle"

    apply from: "${rootProject.projectDir}/gradle/publish-properties.gradle"
    apply from: "${rootProject.projectDir}/gradle/publish-jar.gradle"

    apply from: "${rootProject.projectDir}/gradle/spotless.gradle"
    apply from: "${rootProject.projectDir}/gradle/spotbug.gradle"


    targetCompatibility = 1.8
    sourceCompatibility = 1.8

    tasks.withType(Javadoc).all {
        enabled = false
    }

    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    [compileJava, compileTestJava].each() {
        //it.options.compilerArgs += ["-Xlint:unchecked", "-Xlint:deprecation", "-Xlint:-options"]
        it.options.encoding = "UTF-8"
    }

    repositories {
        mavenLocal()
        maven { url 'http://127.0.0.1:8089/repository/fly-snapshots/' }
        maven { url 'http://127.0.0.1:8089/repository/fly-releases/' }
        maven { url 'https://maven.aliyun.com/repository/public' }
        maven { url 'https://maven.aliyun.com/repository/spring' }
        maven { url 'https://maven.aliyun.com/repository/spring-plugin' }
        mavenCentral()
    }

    dependencyManagement {
        dependencies {
            imports {
                mavenBom "com.github.liaomengge:base-platform-bom:${version}"
            }
        }
        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds'
            cacheDynamicVersionsFor 0, 'seconds'
        }
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}